;; def* forms of macro and lambda

(define defmacro
  (macro (name args & body)
    '(define ~name (macro ~args ~@body))))

(defmacro defn (name args & body)
  '(define ~name (lambda ~args ~@body)))

;; c[a|d]+r

(defn cadr (l) (car (cdr l)))
(defn cddr (l) (cdr (cdr l)))

;; assertion functions

(defn nil? (v) (= v ()))

;; let

(defn let-fn (bindings body)
  (cond
   ((nil? (cdr bindings))
    '((lambda (~(car (car bindings)))
        ~@body)
      ~(cadr (car bindings))))
   (else
    '((lambda (~(car (car bindings)))
        ~(let-fn (cdr bindings) body))
      ~(cadr (car bindings))))))

(defmacro let (bindings & body)
  (let-fn bindings body))

;; boolean logic

(define not
  (lambda (x)
    (cond (x false)
          (else true))))

(defmacro and (x & rest)
  (cond ((nil? rest) x)
        (else '(let ((v ~x))
                 (cond (v (and ~@rest)) (else v))))))

(defmacro or (x & rest)
  (cond ((nil? rest) x)
        (else '(let ((v ~x))
                 (cond (v v) (else (or ~@rest)))))))

;; map/reduce

(defn map (f l)
  (foldl (lambda (acc e) (cons (f e) acc)) () l))

(defn filter (f l)
  (foldl (lambda (acc e)
           (cond ((f e) (cons e acc))
                 (else acc)))
         () l))

(defn reduce (f l & acc)
  (cond ((nil? acc) (foldr f (f (car l) (cadr l)) (cddr l)))
        (else (foldr f (car acc) l))))

;; testing

(define is
  (macro (form)
    '(cond ((not ~form)
            (print "Assertion failed:" (quote ~form))))))

;; math

(defn even? (n)
  (cond ((and (int? n) (= 0 (rem n 2))) true)
        (else false)))

(defn odd? (n)
  (not (even? n)))
